#+title: Dynamic terminal title for Elvish
#+author: Diego Zamboni
#+email: diego@zzamboni.org

This module allows setting the terminal title dynamically using ANSI
escape codes. By default the current directory is shown, and the name
of the current command while one is executing.

This file is written in [[http://www.howardism.org/Technical/Emacs/literate-programming-tutorial.html][literate programming style]], to make it easy
to explain. See [[file:terminal-title.elv][terminal-title.elv]] for the generated file.

* Table of Contents                                            :TOC:noexport:
- [[#usage][Usage]]
- [[#implementation][Implementation]]

* Usage

Install the =elvish-modules= package using [[https://elvish.io/ref/epm.html][epm]]:

#+begin_src elvish
  use epm
  epm:install github.com/zzamboni/elvish-modules
#+end_src

In your =rc.elv=, load this module:

#+begin_src elvish
  use github.com/zzamboni/elvish-modules/terminal-title
#+end_src

Call =terminal-title:setup= to set up the corresponding hooks:

#+begin_src elvish
  terminal-title:setup
#+end_src

By default the terminal title will show the name of the command being
executed or the word ="elvish"=, followed by the current path. You can
customize the titles by setting the
=$terminal-title:title-during-prompt= and
=$terminal-title:title-during-command= variables to lambdas that
determine the title to set while the prompt is displayed (i.e. while
you are typing a command) and while a command is being executed,
respectively. These are their default values:

#+name: title-during-prompt-default
#+begin_src elvish
  title-during-prompt = {
    put "elvish "(tilde-abbr $pwd)
  }
#+end_src

#+name: title-during-command-default
#+begin_src elvish
  title-during-command = [cmd]{
    put (re:split '\s' $cmd | take 1)" "(tilde-abbr $pwd)
  }
#+end_src

* Implementation
:PROPERTIES:
:header-args:elvish: :tangle (concat (file-name-sans-extension (buffer-file-name)) ".elv")
:header-args: :mkdirp yes :comments no
:END:

#+BEGIN_SRC elvish :exports none
  # DO NOT EDIT THIS FILE DIRECTLY
  # This is a file generated from a literate programing source file located at
  # https://github.com/zzamboni/elvish-modules/blob/master/terminal-title.org.
  # You should make any changes there and regenerate it from Emacs org-mode using C-c C-v t
#+END_SRC

We load the [[https://github.com/zzamboni/elvish-modules/blob/master/prompt_hooks.org][prompt_hooks]] and [[https://elvish.io/ref/re.html][re]] libraries.

#+begin_src elvish
  use ./prompt_hooks
  use re
#+end_src

The ANSI escape sequences to start/end the title setting can be
configured in the =$terminal-title:start= and =$terminal-title:end=
variables. The default values work with [[http://tldp.org/HOWTO/Xterm-Title-3.html][xterm]] terminal types
(including Terminal.app on macOS with the =xterm-256color= terminal
type, which I use), but you can change them if needed.

#+begin_src elvish
  start = "\e]0;"
  end = "\007"
#+end_src

The =set-title= function takes a string and sets it as the current
terminal title.

#+begin_src elvish
  fn set-title [title]{
    print $start$title$end > /dev/tty
  }
#+end_src

The =$terminal-title:title-during-prompt= and
=$terminal-title:title-during-command= variables contain lambdas that
determine the title to set while the prompt is displayed (i.e. while
you are typing a command) and while a command is being executed,
respectively.

=title-during-prompt= takes no parameters, and by default shows the word
"elvish" followed by the current directory.

#+begin_src elvish :noweb yes
  <<title-during-prompt-default>>
#+end_src

=title-during-command= received one parameter containing the command
that will be executed. By default shows the command name followed by
the current directory.

#+begin_src elvish :noweb yes
  <<title-during-command-default>>
#+end_src

The =setup= function sets up the corresponding prompt hooks.

#+begin_src elvish
  fn setup {
    prompt_hooks:add-before-readline {
      set-title ($title-during-prompt)
    }
    prompt_hooks:add-after-readline [cmd]{
      set-title ($title-during-command $cmd)
    }
  }
#+end_src
